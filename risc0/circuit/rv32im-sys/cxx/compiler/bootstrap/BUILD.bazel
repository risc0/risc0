load("@zirgen//bazel/rules/zirgen:edsl-defs.bzl", "build_circuit")

cc_library(
    name = "extract_zkr",
    srcs = ["extract_zkr.cpp"],
    hdrs = ["extract_zkr.h"],
    deps = [
        "//rv32im/circuit",
        "//verify/info",
        "@zirgen//zirgen/circuit/predicates:lib",
        "@zirgen//zirgen/compiler/codegen",
    ],
)

cc_test(
    name = "test_zkr",
    size = "large",
    srcs = ["test_zkr.cpp"],
    data = [
        "//rv32im/test:benchmark_kernel",
    ],
    deps = [
        ":extract_zkr",
        "//hal/pick",
        "//prove",
        "//verify",
    ],
)

ZKRS = [
    ("lift_rv32im_m3_" + str(po2))
    for po2 in range(12, 25)
]
ZKRS += [
    ("lift_rv32im_m3_povw_" + str(po2))
    for po2 in range(12, 25)
]

OTHER_ZKRS = [
    "identity",
    "join_povw",
    "join_unwrap_povw",
    "join",
    "resolve_povw",
    "resolve_unwrap_povw",
    "resolve",
    "test_recursion_circuit",
    "union",
    "unwrap_povw",
]

[filegroup(
    name = n + ".zkr",
    srcs = ["@zirgen//zirgen/circuit/predicates:" + n + ".zkr"]
) for n in OTHER_ZKRS]

build_circuit(
    name = "gen_zkr",
    srcs = ["gen_zkr.cpp"],
    outs = [fn for zkr in ZKRS for fn in [
        zkr + ".zkr",
    ]],
    deps = [
        ":extract_zkr",
    ]
)

filegroup(
    name = "zkr",
    srcs = [
        zkr + ".zkr" for zkr in ZKRS + OTHER_ZKRS
    ]
)

cc_binary(
    name = "bootstrap",
    srcs = [
        "main.cpp",
        "poly_ext.cpp",
        "taps.cpp",
    ],
    deps = [
        "//compiler/extractor",
        "//rv32im/circuit",
        "//rv32im/emu",
        "//verify/info",
    ],
)
